
~///////////////////////////////////////////////////////////////////////
栞 サンプル始め

クリア //以前生成された全てのリソースを解除
スクリプト textbox_sample.txt textbox_noautowait //ボタン文字は文字窓を必要

ボタン btn1 btn_sel.png 262 30 90 //表示のみ

ボタン btn2 btn_sel.png btn_sel@.png 262 100 90 //触ると変化

ボタン btn3 btn_sel.png btn_sel@.png btn_sel.png 262 170 90 //触ると変化＋クリック時変化

ボタン基本資料 btn_sel.png btn_sel@.png btn_sel.png
~ボタン btn4 262 240 90 //ボタン基本資料を適応：使用素材の指定不要

"[待機 ボタン選択]
..ボタンがクリックされるまで待ちます。

~待機 ボタン選択 //ボタンが選択(クリック)されるまで待つ

アウト btn1, btn2, btn3, btn4

///////////////////////////////////////////////////////////////////////
//ボタンにコマンド追加 (クリック時発動)
//ボタンの最後に望むコマンドを付けると、クリック時に作動します
//(Light.vn内で使用可能な全てのコマンド適用可能)

ボタン基本資料 btn_sel.png btn_sel@.png btn_sel.png

ボタン btn1 262 30 90 "ふははははは！ \w
~ボタン文字 btn1 r-mplus-1c-m.ttf 24  "クリック時台詞表示

~ボタン btn2 262 100 90 ジャンプ サンプル始め
~ボタン文字 btn2 r-mplus-1c-m.ttf 24  "クリック時「サンプル始め」栞ヘ

~ボタン btn3 262 170 90 ジャンプ tut_text.txt 
~ボタン文字 btn3 r-mplus-1c-m.ttf 24  "クリック時「tut_text.txt」へ

~ボタン btn4 btn_sel2.png btn_sel2@.png 262 240 90 背景音 eok.ogg 反復 
~ボタン文字 btn4 r-mplus-1c-m.ttf 24  "クリック時背景音再生

~//注：ボタン基本資料を宣言しても、btn4のように使用素材を特定すると
//特定された素材が使用されます。

"[ボタン コマンド]
..ボタンをクリックすると登録された
.コマンドがそれぞれ作動します。

~待機 ボタン選択


~//お疲れ様でした！
