背景 bg1 Classroom_04_day.jpg
背景 Classroom_01_day.jpg
//注：背景レイヤ：0 (自動)
//クリア

~///////////////////////////////////////////////////////////////
//Light.vn サンプルスクリプト
// '//'（半角）はコメントを意味します
栞 sample_start

変数 スクリプト名 = '紹介'
文字 script_str 890 23 81  r-mplus-1c-m.ttf 24 '[ {{スクリプト名}} ]

~背景音 eok.ogg 反復 
イン 背景音 5000

絵 cg1 test1.png -20 0 60
絵 alice1 scg2.png 100 180 80
イン alice1 300

//右クリック時メニューがロードされるようにする
//スクリプト system\sys_menu.txt

//これから使用する文字窓（テキストボックス)をロード、
//ctrlを押す事でスキップモードon/off
スクリプト textbox_sample.txt textbox_noautowait 
//スクリプト textbox_sample.txt textbox_vertical //文字方向 縦

//文章表示:
//"→改ページ　-"→ページ続き　.→改行

変数 話者 = 'Light.vn'
//注：変数は宣言より前行からテストプレイを開始しないと
//ちゃんと作動されません。

"[ {{ 話者 }} ]
.こんにちは、Light.vn（ライト・ヴィエン）です。\w
.このサンプルを始めて見る方たちは先ずエディターである
.LightEditor.exe上で
起動させる事をお勧めします。\w

"[ {{ 話者 }} ]
.このサンプルはLight.vnで使用可能な命令語、及びエディターの使い方を
紹介し、ノベル製作時に参考出来るスクリプトを
提供する為に作られました。\w

~//"例えば、右クリックをするとメニューの表示が始まり (system_menu.txt)、
//-"\nまたマウスウィールを上に回すとバックログの表示 (system_backlog.txt) 
//-"\nを発生させるよう本サンプルのスクリプトを組んでいます。
//-"\n是非エディター上でそれぞれのフィアルを確認してみてください。

"[ {{ 話者 }} ]
.Light.vnの製作趣旨は
.「誰でも容易くノベルを作られる"環境"を提供する」事にあります。\w

"[ {{ 話者 }} ]
.そうするために出来る限り
.１．テキスト編集
.２．演出（カメラ、イメージ、音楽等）
.を簡略化し、その上スクリプト結果を即時確認・修正出来るよう
.エディター上にプレビュー機能、命令語リストを提供しております。\w

"[ {{ 話者 }} ]
.目が遠くなる英語も排除し、命令語を全て日本語化する事で
.スクリプトの内容がもっと直感的に把握できるようにしました。\w

"[ {{ 話者 }} ]
.バグ等に思われる物を発見したり、何らかの機能が足りない、
.あるいはこれを具現して欲しいーという機能がありましたら
.Light.vnのgithub page「https://github.com/hsdk123/Light.vn」、
.又製作者のツイッター「@daegon137」に是非ご連絡ください。\w

"[ {{ 話者 }} ]
.ちなみにエディターの下にＦＰＳ（ノベルアップデート速度）\n情報もありますので
急に下がったり、特に５０以下に落ちた場合は\n是非ともフィードバックを
お願い致します。 \w

~///////////////////////////////////////////////////////////////
栞 tutorial_jump
移動 alice1 10 180 500
変数 話者 = 'Light.vn'
"[ {{ 話者 }} ]
..ではー、具体的なチュートリアルに移りましょうかー。

~//それぞれのチュートリアルにジャンプするボタンを設定
//注: 他のスクリプトに'jump'（ジャンプ）する命令群は
//プレイ（或いはテストプレイ）状態にだけ起動します。

ボタン基本資料 btn_sel.png btn_sel@.png btn_sel.png
ボタン jump_txt 262 50 90 ジャンプ tut_text.txt tut_start 
ボタン文字 jump_txt r-mplus-1c-m.ttf 24 "テキストチュートリアル

~ボタン jump_basic 262 120 90 ジャンプ tut_basic.txt   
ボタン文字 jump_basic r-mplus-1c-m.ttf 24 "イメージ、演出 チュートリアル

~ボタン jump_var 262 190 90　ジャンプ tut_var.txt  
ボタン文字 jump_var r-mplus-1c-m.ttf 24 "変数操作 例

~ボタン btn_end 262 260 90 
ボタン文字 btn_end r-mplus-1c-m.ttf 24 "ゲームを終了する

~ボタンタッチ時 jump_txt スクリプト effects.txt btnTouch1 
~ボタンタッチ時 jump_basic スクリプト effects.txt btnTouch2 
~ボタンタッチ時 jump_movie 文字色 jump_movie 0 255 255
~ボタン未タッチ時 jump_movie 文字色 jump_movie 255 255 255

//ボタンが選択されるまで待つ
待機 ボタン選択

~効果音 se_display se_maoudamashii_onepoint26.ogg 
"..ゲームを終了します。\w

~///////////////////////////////////////////////////////////////
栞 game_end

//文字進行：普通(0)に強制的に戻す
文字進行 0

//全てのリソース（絵、背景音楽、等）
//が完全にフェードアウトするまで待つ(シンクロ命令)
.アウト 全部 3500

文字窓 credits_box 60 350 81 904 0 r-mplus-1c-m.ttf 24
使用文字窓 credits_box
文字速度 15
文字ボールド true
文字陰 1
文字ワードラップ false

"Light.vn 製作委員会 2014
.ノベル製作活動 応援中

~.移動 credits_box 60 -100 1000

-"\w

~.アウト credits_box 1300

//アプリケーション（エディターじゃなく）
//でゲームをプレイする場合
//以下の命令でプログラムを終了

ゲームエンド

//注意：スクリプトの最後に１行以上の空き空間を残してください。
