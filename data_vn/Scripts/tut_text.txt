背景 bg0 Classroom_05_day.jpg

栞 tut_start

ロールアウト fade_swirl.png 800

//クリアー：以前生成された全てのリソースを解除 （絵、背景、バトン、背景音楽・・・）
//注：クリアーはスクリプト編集時には作動しません。
クリア
~バックログリセット

//注:　背景はゲームの解像度と同じ規格ににストレッチされます
背景 bg0 Classroom_05_day.jpg

ロールイン fade_swirl.png 800

背景音 snowsnow.ogg 反復
//イン 背景音 5000

//スクリプト system\sys_menu.txt
スクリプト textbox_sample.txt textbox_noautowait

絵 alice1 scg2.png 400 180 80
イン alice1 300

"テキストチュートリアルへようこそ。
.このチュートリアルの目的はテキストの表示と関連命令群の紹介にあります。\w

~///////////////////////////////////////////////////////////////

"<シナリオ適用>\w

"先ず、行の真っ先に
."を置くと「改ページ」（既存表示文章を消して文章を開始する）
.-"を置くと「ページ続き」（既存表示文章を維持、その後に続く文章）
.を意味します。\w

"例えば、この文章は行の真っ先に「”」置いたから既存の文章が消えています。\w

-"この文章は、行の真っ先に「-”」を置いたから既存の文章を消せずに後に続いています。\w

"ですが、大抵の場合は「-"」（「ページ続き」命令語）を使う必要はありません。\w

"「"」や「-"」で行が始まると、
それ以降、行の真っ先に「～」が出るまでの全ての行を
自動的に「ページ続き」として認識するからです。
(「～」の説明は少し後にします）\w
結果的に、こういう少し長い長文も楽に適用できるようになりました。\w

"其の上、シナリオ適用時、改行の必要がある場合、
.シナリオ行の真っ先に「.」を置くと、「.」の数程便利に改行されます。\w
...こんな風に。(「.」３回で３行）\w

~//注：「.」を行の真っ先に入れず、行の最初にスペース一つでも入れてから
//置くと、改行文字として認識されません。

"「"」や[-"」を一度使用すると「～」が出るまでの行は
.全て[シナリオテキスト]として自動に認識される点と、
.行の真っ先に「.」を置くと簡単に改行される点を利用すると
.シナリオ適用・修正・確認等の作業は簡単に出来るかと思います。\w

"この上Light.vnエディターの「プレビュー画面」を利用すると
.テキストを適用しながら美的に配置する作業だって相当楽に出来ます。\w

~///////////////////////////////////////////////////////////////

"<シナリオ適用（詳細）>\w

"んじゃ、先話した「～」は何なの？という質問に答えると、
.「～」が行の真っ先に出ると、Light.vnは
.「この行からのスクリプト行は全てシナリオじゃなくて、
.命令語（コマンド）だね」と認識します。\w

"簡単な例として、

.移動 alice1 230 200 500
.移動 alice1 30 0 500

.この場合、最初の「移動」命令行に「～」文字が無いため
.シナリオ行（ページ続き）として認識されました。\w

~.移動 alice1 230 380 500
移動 alice1 30 180 500

"今回は最初の「移動」命令行に「～」文字を真っ先に置いた為
.最初の移動命令と、其の後の移動命令もちゃんと作動しました。\w

"ちなみに、Light.vnに「シナリオテキスト」として認識される行は
.全てエディター上で青にハイライトされます。\w
其の行がシナリオとして認識されれてるのか、命令行として
認識されてるのかはこれで簡単に把握できます。\w

~///////////////////////////////////////////////////////////////

"<テキスト操作 -基本->\w

"ルール１）テキスト表示は文字窓（テキストボックス）を必要とします。\w
.文章の表示、及びテキスト関連命令群全ては先に正当な文字窓の宣言を
必要とします。\w

"例えば・・・。\w
~使用文字窓　存在しない文字窓

"この文章は表示されません。

~//注: 「textbox0」は上の「スクリプト textbox_sample.txt textbox_def」から呼びました

使用文字窓 textbox0
-".これをエディター上で見ると、上の文章が表示されてない事に気付いたはずです。\w
 理由は「存在しない文字窓」、存在しない文字窓の使用は不可能だからです。\w
"その反面、現在この文章が見えている理由は正当な文字窓である
.「textbox0」の使用宣言を先にしてるからです。\w

~使用文字窓　存在しない文字窓
//これは適用されません
文字サイズ 40

使用文字窓 textbox0
"これは他のテキスト命令群（文字フォント、文字速度など）
にも同じく適用されます。
 (各コマンドに関しての詳しい情報はエディター左側の
.「コマンド使用情報」で確認できます)\w

~文字サイズ 40
"正当な文字窓を使ってる今、これはちゃんと大きく見えるはずです。\w

~//update必要

"注：文字サイズは現在表示されてる全てのテキストに適用されます。\w
~文字サイズ 24
-" この文章は「文字サイズ 40」が適用されてるときに始めました.\w
 でもつい先（文章の中間に） 「文字サイズ 24」を適用した為全ての
 テキストが文字サイズ 24を適用されています. \w

~文字整列 中央
"ちなみに、文字の左・右・中央整列も
.問題なく作動し、\w

~文字整列 左
~文字行サイズ 70
"文字行サイズも
.自由に
.設定出来ます。\w

~文字整列 左
~文字行サイズ 50
アウト textbox0

~///////////////////////////////////////////////////////////////

スクリプト textbox_sample.txt textbox_noautowait

"ルール２）テキストは正当なフォントを必要とします。\w
.これは日本語等の非英語言語の表示時に特に重要となります。\w
.たとえば・・・。\w

~文字フォント　arial.ttf
"これが一瞬四角い模様の連続が見えたはずです。\w
~文字フォント r-mplus-1c-m.ttf
-".これは日本語文字を含まないフォントである「arial.ttf」を
一瞬適用したからです。\w

"逆に言えば、有効なフォントさえあればあらゆる言語の
表示ができると言う事になります。\w

~文字フォント DFYearSong-B5.TTC
"只要有字體的話,中文也可用。（繁體，簡體無關！）\w
~文字フォント r-mplus-1c-m.ttf
"正当なフォントさえあれば中国語も無理なく表示されます。\w

~文字フォント NanumGothic.ttf
"한글도 이렇듯 제대로 표시됩니다. \w
.ハングルもちゃんとー！ \w

~文字フォント r-mplus-1c-m.ttf
"バックログも同じく文字窓を使っていますのでフォントに
関してはご注意ください。\w
（本サンプルではマウスのフィールを上に回すと
バックログが表示されるようにしました。バックログは日本語フォントが
適用されている為先のハングル部分等の表示がちゃんと出来ていません。）\w

~///////////////////////////////////////////////////////////////

"ルール３）待機に関して。\w
.普通ノベルゲームでは文章が終わるたびにユーザーの入力を待った後
次の文章を表示します。\w

"Light.vnではこの待機効果を三つの方法で具現できます。
.1. \ w 略語
.2. 文字自動待機 命令語
.3. 待機 命令語 \w

"1. シナリオ行に「\ w」略語を入れると、
.その行の表示が終わったらLight.vnが待機し、ユーザの入力を待ちます。\w

"2. 「文字自動待機」命令を「on」に設定すると、
.シナリオ行が終わるたびに自動的にLight.vnが待機します。\w

~文字自動待機 on
-".こんな風に。
~文字自動待機 off

"3. 「待機」命令を使うと、
.Light.vnが待機状態に入ります。\w

-".こんな風に。
~待機

"どの待機方法を使うのかは全く製作者の自由です。
.ですが、何らかの特殊な演出効果を必要とする場合でなければ、
.待機したいシナリオ行に「\ w」を入れる事が簡単かと思います。\w

~.アウト alice1 300

~//注：シナリオ行に「\c」(c: 'continue')を入れると、
//「文字自動待機 on」にも関わらず
//Light.vnはその行に待機せず次の行を読み込みます。

//注：シナリオ行の中間に「\n」(n: 'new line')を入れると
//シナリオテキストの其の部分からは改行します。

~///////////////////////////////////////////////////////////////
"<追加：文字窓を応用してチャット雰囲気演出>\w

~スクリプト textbox_sample.txt textbox_fadeout

絵 alice1 scg2.png 500 180 80
絵 ace1 scg1.png 100 0 80
イン alice1 300
イン ace1 300

文字窓 t_left 100 340 81 300 0 r-mplus-1c-m.ttf 28
使用文字窓 t_left
文字窓スキン sys\textbox_left.png 0 30
文字自動待機 off
"[エース(Ace)]
.フハハハ！俺様の表現の自由は無限に等しい！\w

~移動 alice1 400 180 1000

~文字窓 t_right 600 350 81 300 0 r-mplus-1c-m.ttf 24
使用文字窓 t_right
文字窓スキン sys\textbox_left.png 500 20
"[アリス(Alice)]
.うるさいぞロリコン.\w

~使用文字窓 t_left
文字サイズ 35
"[エース(Ace)]
.ナニ!? \w

~使用文字窓 t_right
文字サイズ 24
"[アリス(Alice)]
.幼女の面前で裸になってんじゃねーつってんだよ、
警察に訴えるぞコノヤロウ。\w

~使用文字窓 t_left
文字サイズ 30
"[エース(Ace)]
.俺様はそのような破廉恥な事をした覚えが無いぞ！\w

~使用文字窓 t_right
"[アリス(Alice)]
.いや、頭の中じゃもう何度もやってみたはず。
只怖くて実際実行してみなかっただけでしょう？ ^^\w

~使用文字窓 t_left
"[エース(Ace)]
. ....\w

~アウト t_left 300
.アウト t_right 300

///////////////////////////////////////////////////////////////
スクリプト textbox_sample.txt textbox_noautowait
//.イン textbox0 300
"<追加：文字窓応用2>\w

~スクリプト textbox_sample.txt textbox_fadeout

文字窓 t_large 100 60 80 824 500 r-mplus-1c-m.ttf 28
使用文字窓 t_large
文字窓スキン sys\textbar_full.png 0 0
文字自動待機 off

シスボタン btn_tSkip sys\txt_skip.png sys\txt_skip@.png sys\txt_skip@.png 900 321 90 文字進行 2
シスボタン btn_tAuto sys\txt_auto.png sys\txt_auto@.png sys\txt_auto@.png 902 361 90 文字進行 1

"その後の話・・・・。\w
.エースは結局逮捕された。\w
..警察に捕まる際彼は、\w
.「俺だって小学校の前で裸になる権利はあるんだ！」
.そう叫んだと伝われる。\w
.. - 人生終了 -\w

~アウト btn_tSkip
アウト btn_tAuto
.アウト t_large 300
アウト ace1 300

///////////////////////////////////////////////////////////////
//注: textbox0 は textbox_sample.txtの中に定義されています
スクリプト textbox_sample.txt textbox_noautowait

"ご覧の通り文字窓の切り替えを少し応用すれば
.会話的な演出等が容易く出来るようになります。\w
.ノベル上でユーザーが殆ど見るのがテキストである
.事を考慮して文字窓を使っていろいろ試してみてくださいー。\w

~///////////////////////////////////////////////////////////////

ジャンプ start0.txt tutorial_jump
